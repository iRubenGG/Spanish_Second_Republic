title: Unios Hermanos Proletarios
new-page: true
is-card: true
view-if: iron_front_formed == 1 and iron_front_timer = 0
on-arrival: iron_front_timer = 7; month_actions += 1; rb_militarization_cost = 2 if rb_militancy > 0.1 else rb_militarization_cost = 1; rb_militarization_cost = 3 if rb_militancy > 0.25
card-image: img/iron_front.png
frequency: 200
tags: party_affairs, antifascism

= Unios Hermanos Proletarios

Unios Hermanos Proletarios is our new antifascist coordination front, consisting of the PSOE, the trade unions, and workers' militias. How should we use this organization?

[? if workers_psoe < 45 or workers_psoe < workers_falange or workers_psoe < workers_pce : Unfortunately, the alliance's effectiveness is dragged down by the PSOE's unpopularity, as the PSOE is no longer the dominant party among the workers. Many militia members have left, and many workers are not interested. ?]

- @defense: Rally for defense against the fascists with militias.
- @pacifism: Rally for pacifism, against nationalism and war.
- @republic: Rally to save the democratic Republic against dictatorship.
- @economics: Rally for economic change and workers' protections.
- @antisemitism
- @easy_discard

@defense
subtitle: -[+ rb_militarization_cost +] resources
choose-if: resources >= rb_militarization_cost
on-arrival: resources -= rb_militarization_cost; rb_strength += 50; rb_militancy += 0.05; ceda_relation -= 5 if historical_mode

The organization rallies for the militant defense of the Republic against the right-wing paramilitaries, strengthening workers' militias.

[? if historical_mode : The CEDA decries the militarization of the workers' organizations and Unios Hermanos Proletarios, and tensions increase. ?]


@pacifism
on-arrival: pacifism += 1; nationalism -= 5 if pacifism >= 3; workers_psoe += 5*(1-dissent) if pacifism >= 4; unemployed_psoe += 4*(1-dissent) if pacifism >= 4; new_middle_psoe += 4*(1-dissent) if pacifism >= 4

The organization rallies against nationalism and war.

@republic
on-arrival: pro_republic += 5*(1-dissent); democratization += 1; workers_psoe += 5*(1-dissent) if democratization >= 4; unemployed_psoe += 4*(1-dissent) if democratization >= 4; new_middle_psoe += 4*(1-dissent) if democratization >= 4

The organization rallies in defense of the democratic Republic and against dictatorship.

@economics
choose-if: return_to_normalcy = 0
unavailable-subtitle: The economic crisis is largely over now, and the public is no longer interested in hearing about it.
view-if: wtb_adopted == 1
on-arrival: workers_psoe += 8*(1-dissent); unemployed_psoe += 8*(1-dissent); new_middle_psoe += 5*(1-dissent); old_middle_psoe += 3*(1-dissent); rural_psoe += 3*(1-dissent);

Economic reform is the rallying point for the entire PSOE and its affiliated organizations, including Unios Hermanos Proletarios.

@antisemitism
title: Rally against reactionary violence and in defense of all minorities.
view-if: confronting_antisemitism > 1
on-arrival: nationalism -= 4*(1-dissent); pro_republic += 3*(1-dissent); confronting_antisemitism += 1; workers_falange -= 4*(1-dissent); unemployed_falange -= 4*(1-dissent);

The Falange and other far-right political movements are increasingly turning their hatred against vulnerable groups. Unios Hermanos Proletarios rallies in defense of all victims of this violence and the broader community.
